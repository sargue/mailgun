plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'net.sargue'
archivesBaseName = 'mailgun'
version '2.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.glassfish.jersey.core:jersey-client:3.0.9'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:3.0.9'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:3.0.9'
    testImplementation 'com.github.tomakehurst:wiremock:1.57'
    testImplementation 'org.awaitility:awaitility:2.0.0'
    testImplementation 'org.slf4j:slf4j-simple:1.7.21'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mailgun'
            from components.java
            pom {
                name = 'Mailgun java library'
                description = 'Library to send email messages from Java using the Mailgun service'
                url = 'https://github.com/sargue/mailgun'

                scm {
                    connection = 'scm:git:git@github.com:sargue/mailgun.git'
                    developerConnection = 'scm:git:git@github.com:sargue/mailgun.git'
                    url = 'https://github.com/sargue/mailgun'
                }

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'sargue'
                        name = 'Sergi Baila'
                        email = 'sergibaila@protonmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
