plugins {
    id "com.jfrog.bintray" version "1.6"
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'

group 'net.sargue'
archivesBaseName = 'mailgun'
version '2.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

ext {
    jerseyVersion = "2.25.1"
}

dependencies {
    compileOnly 'org.slf4j:slf4j-api:1.7.21'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.11.1'

    compileOnly 'org.glassfish.jersey.core:jersey-client:' + jerseyVersion
    compileOnly 'org.glassfish.jersey.media:jersey-media-multipart:' + jerseyVersion

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testImplementation 'org.mockito:mockito-core:2.21.0'
    testImplementation 'com.google.guava:guava:26.0-jre'
    testImplementation 'org.slf4j:slf4j-simple:1.7.21'
    testImplementation('com.github.tomakehurst:wiremock:2.18.0') {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation 'org.awaitility:awaitility:3.1.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'

    testImplementation 'org.glassfish.jersey.core:jersey-client:' + jerseyVersion
    testImplementation 'org.glassfish.jersey.media:jersey-media-multipart:' + jerseyVersion
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Mailgun java library',
                'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Mailgun java library'
            description 'Library to send email messages from Java using the Mailgun service'
            url 'https://github.com/sargue/mailgun'

            scm {
                connection 'scm:git:git@github.com:sargue/mailgun.git'
                developerConnection 'scm:git:git@github.com:sargue/mailgun.git'
                url 'git@github.com:sargue/mailgun.git'
            }

            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    id 'sargue'
                    name 'Sergi Baila'
                    email 'sargue@gmail.com'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'net.sargue:mailgun'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/sargue/mailgun.git'
        version {
            name = project.version
            desc = 'Mailgun java library'

            gpg {
                sign = true
                passphrase = project.hasProperty('BINTRAY_GPG') ? BINTRAY_GPG : ''
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'mailgun'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.branch.name', 'v2.x'
        property 'sonar.login', SONARQUBE_TOKEN
        property 'sonar.exclusions', 'src/main/java/net/sargue/mailgun/log/Logger.java'
    }
}

jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'net/sargue/mailgun/log/**')
        })
    }
}
