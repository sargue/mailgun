apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group 'net.sargue'
archivesBaseName = 'mailgun'
version '1.0.0'

def NEXUS_USERNAME = hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ''
def NEXUS_PASSWORD = hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ''

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.glassfish.jersey.core:jersey-client:[2.22,)'
    compile 'org.glassfish.jersey.media:jersey-media-multipart:[2.22,)'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Mailgun java library',
                'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }

            pom.project {
                name 'Mailgun java library'
                packaging 'jar'
                description 'Library to send email messages from Java using the Mailgun service'
                url 'https://github.com/sargue/mailgun'

                scm {
                    connection 'scm:git:git@github.com:sargue/mailgun.git'
                    developerConnection 'scm:git:git@github.com:sargue/mailgun.git'
                    url 'git@github.com:sargue/mailgun.git'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'sargue'
                        name 'Sergi Baila'
                        email 'sargue@gmail.com'
                    }
                }
            }
        }
    }
}
