plugins {
    id "com.jfrog.bintray" version "1.6"
    id "org.sonarqube" version "2.0.1"
}

apply plugin: 'java'
apply plugin: 'maven'

group 'net.sargue'
archivesBaseName = 'mailgun'
version '1.3.1'

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile 'org.glassfish.jersey.core:jersey-client:[2.22,)'
    compile 'org.glassfish.jersey.media:jersey-media-multipart:[2.22,)'
    testCompile 'com.github.tomakehurst:wiremock:1.57'
    testCompile 'org.awaitility:awaitility:2.0.0'
    testCompile 'org.slf4j:slf4j-simple:1.7.21'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Mailgun java library',
                'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Mailgun java library'
            description 'Library to send email messages from Java using the Mailgun service'
            url 'https://github.com/sargue/mailgun'

            scm {
                connection 'scm:git:git@github.com:sargue/mailgun.git'
                developerConnection 'scm:git:git@github.com:sargue/mailgun.git'
                url 'git@github.com:sargue/mailgun.git'
            }

            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    id 'sargue'
                    name 'Sergi Baila'
                    email 'sargue@gmail.com'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'net.sargue:mailgun'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/sargue/mailgun.git'
        version {
            name = project.version
            desc = 'Mailgun java library'

            gpg {
                sign = true
                passphrase = project.hasProperty('BINTRAY_GPG') ? BINTRAY_GPG : ''
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'mailgun'
        property 'sonar.host.url', 'https://sonarqube.com'
        property 'sonar.login', SONARQUBE_TOKEN
    }
}